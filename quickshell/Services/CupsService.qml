pragma Singleton

pragma ComponentBehavior

import QtQuick
import Quickshell
import Quickshell.Io
import qs.Common

Singleton {
    id: root

    property int refCount: 0

    property var printerNames: []
    property var printers: []
    property string selectedPrinter: ""

    property bool cupsAvailable: false
    property bool stateInitialized: false

    signal cupsStateUpdate

    readonly property string socketPath: Quickshell.env("DMS_SOCKET")

    Component.onCompleted: {
        if (socketPath && socketPath.length > 0) {
            checkDMSCapabilities()
        }
    }

    Connections {
        target: DMSService

        function onConnectionStateChanged() {
            if (DMSService.isConnected) {
                checkDMSCapabilities()
            }
        }
    }

    Connections {
        target: DMSService
        enabled: DMSService.isConnected

        function onCupsStateUpdate(data) {
            console.log("CupsService: Subscription update received")
            getState()
        }

        function onCapabilitiesChanged() {
            checkDMSCapabilities()
        }
    }

    function checkDMSCapabilities() {
        if (!DMSService.isConnected) {
            return
        }

        if (DMSService.capabilities.length === 0) {
            return
        }

        cupsAvailable = DMSService.capabilities.includes("cups")

        if (cupsAvailable && !stateInitialized) {
            stateInitialized = true
            getState()
        }
    }

    function getState() {
        if (!cupsAvailable)
            return

        DMSService.sendRequest("cups.getPrinters", null, response => {
                                   if (response.result) {
                                       updatePrinters(response.result)
                                       fetchAllJobs()
                                   }
                               })
    }

    function updatePrinters(printersData) {
        printerNames = printersData.map(p => p.name)

        let printersObj = {}
        for (var i = 0; i < printersData.length; i++) {
            let printer = printersData[i]
            printersObj[printer.name] = {
                "state": printer.state,
                "stateReason": printer.stateReason,
                "jobs": []
            }
        }
        printers = printersObj

        if (printerNames.length > 0) {
            if (selectedPrinter.length > 0) {
                if (!printerNames.includes(selectedPrinter)) {
                    selectedPrinter = printerNames[0]
                }
            } else {
                selectedPrinter = printerNames[0]
            }
        }
    }

    function fetchAllJobs() {
        for (var i = 0; i < printerNames.length; i++) {
            fetchJobsForPrinter(printerNames[i])
        }
    }

    function fetchJobsForPrinter(printerName) {
        const params = {
            "printerName": printerName
        }

        DMSService.sendRequest("cups.getJobs", params, response => {
                                   if (response.result && printers[printerName]) {
                                       let updatedPrinters = Object.assign({}, printers)
                                       updatedPrinters[printerName].jobs = response.result
                                       printers = updatedPrinters
                                   }
                               })
    }

    function getSelectedPrinter() {
        return selectedPrinter
    }

    function setSelectedPrinter(printerName) {
        if (printerNames.length > 0) {
            if (printerNames.includes(printerName)) {
                selectedPrinter = printerName
            } else {
                selectedPrinter = printerNames[0]
            }
        }
    }

    function getPrintersNum() {
        if (!cupsAvailable)
            return 0

        return printerNames.length
    }

    function getPrintersNames() {
        if (!cupsAvailable)
            return []

        return printerNames
    }

    function getTotalJobsNum() {
        if (!cupsAvailable)
            return 0

        var result = 0
        for (var i = 0; i < printerNames.length; i++) {
            var printerName = printerNames[i]
            if (printers[printerName] && printers[printerName].jobs) {
                result += printers[printerName].jobs.length
            }
        }
        return result
    }

    function getCurrentPrinterState() {
        if (!cupsAvailable || !selectedPrinter)
            return ""

        var printer = printers[selectedPrinter]
        return printer.state
    }

    function getCurrentPrinterStatePrettyShort() {
        if (!cupsAvailable || !selectedPrinter)
            return ""

        var printer = printers[selectedPrinter]
        return getPrinterStateTranslation(printer.state) + " (" + getPrinterStateReasonTranslation(printer.stateReason) + ")"
    }

    function getCurrentPrinterStatePretty() {
        if (!cupsAvailable || !selectedPrinter)
            return ""

        var printer = printers[selectedPrinter]
        return getPrinterStateTranslation(printer.state) + " (" + "Reason" + ": " + getPrinterStateReasonTranslation(printer.stateReason) + ")"
    }

    function getCurrentPrinterJobs() {
        if (!cupsAvailable || !selectedPrinter)
            return []

        return getJobs(selectedPrinter)
    }

    function getJobs(printerName) {
        if (!cupsAvailable)
            return ""

        var printer = printers[printerName]
        return printer.jobs
    }

    function getJobsNum(printerName) {
        if (!cupsAvailable)
            return 0

        var printer = printers[printerName]
        return printer.jobs.length
    }

    function pausePrinter(printerName) {
        if (!cupsAvailable)
            return

        const params = {
            "printerName": printerName
        }

        DMSService.sendRequest("cups.pausePrinter", params, response => {
                                   if (response.error) {
                                       ToastService.showError("Failed to pause printer" + " - " + response.error)
                                   } else {
                                       getState()
                                   }
                               })
    }

    function resumePrinter(printerName) {
        if (!cupsAvailable)
            return

        const params = {
            "printerName": printerName
        }

        DMSService.sendRequest("cups.resumePrinter", params, response => {
                                   if (response.error) {
                                       ToastService.showError("Failed to resume printer" + " - " + response.error)
                                   } else {
                                       getState()
                                   }
                               })
    }

    function cancelJob(printerName, jobID) {
        if (!cupsAvailable)
            return

        const params = {
            "printerName": printerName,
            "jobID": jobID
        }

        DMSService.sendRequest("cups.cancelJob", params, response => {
                                   if (response.error) {
                                       ToastService.showError("Failed to cancel selected job" + " - " + response.error)
                                   } else {
                                       fetchJobsForPrinter(printerName)
                                   }
                               })
    }

    function purgeJobs(printerName) {
        if (!cupsAvailable)
            return

        const params = {
            "printerName": printerName
        }

        DMSService.sendRequest("cups.purgeJobs", params, response => {
                                   if (response.error) {
                                       ToastService.showError("Failed to cancel all jobs" + " - " + response.error)
                                   } else {
                                       fetchJobsForPrinter(printerName)
                                   }
                               })
    }

    readonly property var states: ({
                                       "idle": "Idle",
                                       "processing": "Processing",
                                       "stopped": "Stopped"
                                   })

    readonly property var reasonsGeneral: ({
                                               "none": "None",
                                               "other": "Other"
                                           })

    readonly property var reasonsSupplies: ({
                                                "toner-low": "Toner Low",
                                                "toner-empty": "Toner Empty",
                                                "marker-supply-low": "Marker Supply Low",
                                                "marker-supply-empty": "Marker Supply Empty",
                                                "marker-waste-almost-full": "Marker Waste Almost Full",
                                                "marker-waste-full": "Marker Waste Full"
                                            })

    readonly property var reasonsMedia: ({
                                             "media-low": "Media Low",
                                             "media-empty": "Media Empty",
                                             "media-needed": "Media Needed",
                                             "media-jam": "Media Jam"
                                         })

    readonly property var reasonsParts: ({
                                             "cover-open": "Cover Open",
                                             "door-open": "Door Open",
                                             "interlock-open": "Interlock Open",
                                             "output-tray-missing": "Output Tray Missing",
                                             "output-area-almost-full": "Output Area Almost Full",
                                             "output-area-full": "Output Area Full"
                                         })

    readonly property var reasonsErrors: ({
                                              "paused": "Paused",
                                              "shutdown": "Shutdown",
                                              "connecting-to-device": "Connecting to Device",
                                              "timed-out": "Timed Out",
                                              "stopping": "Stopping",
                                              "stopped-partly": "Stopped Partly"
                                          })

    readonly property var reasonsService: ({
                                               "spool-area-full": "Spool Area Full",
                                               "cups-missing-filter-warning": "CUPS Missing Filter Warning",
                                               "cups-insecure-filter-warning": "CUPS Insecure Filter Warning"
                                           })

    readonly property var reasonsConnectivity: ({
                                                    "offline-report": "Offline Report",
                                                    "moving-to-paused": "Moving to Paused"
                                                })

    readonly property var severitySuffixes: ({
                                                 "-error": "Error",
                                                 "-warning": "Warning",
                                                 "-report": "Report"
                                             })

    function getPrinterStateTranslation(state) {
        return states[state] || state
    }

    function getPrinterStateReasonTranslation(reason) {
        let allReasons = Object.assign({}, reasonsGeneral, reasonsSupplies, reasonsMedia, reasonsParts, reasonsErrors, reasonsService, reasonsConnectivity)

        let basReason = reason
        let suffix = ""

        for (let s in severitySuffixes) {
            if (reason.endsWith(s)) {
                basReason = reason.slice(0, -s.length)
                suffix = severitySuffixes[s]
                break
            }
        }

        let translation = allReasons[basReason] || basReason
        return suffix ? translation + " (" + suffix + ")" : translation
    }
}
